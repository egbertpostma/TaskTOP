import "../UPPAAL_stdFunctions.etl";

/*
pre {
    var javaDate = new Native("java.util.Date");
    javaDate.toString().println("Starting CTT2UPPAAL transformation: ");
}

post {
    var javaDate = new Native("java.util.Date");
    javaDate.toString().println("Completed CTT2UPPAAL transformation: ");
}
*/

/**
 * A null task is used to replace the 'optional' operator 
 * with a choice operator of which the right subtask is a null task.
 */

operation createNullTaskTemplate() : Uppaal!Template {
    //// Create template
    var template = new Uppaal!Template();
    template.name = "NullTask";

    //// Add parameters
    
    // Create id paramater
    var var_id = createVariable("id");
    template.addParameter(createDataDeclaration(var_id), false);        
    
    //// Add locations
    
    // Add initial location
    var loc_disabled  = template.createLocation("Disabled", 0, 0);
    var loc_enabled   = template.createLocation("Enabled", 1, 0);
    var loc_suspended = template.createLocation("Suspended", 2, 0);
    var loc_done_1   = template.createLocation("", 1, 1).makeCommitted();
    var loc_done      = template.createLocation("Done", 0, 1);
    
    template.init = loc_disabled;
    
    //// Add edges    
    
    var var_activate = createVariable("activate");
    var var_deactivate = createVariable("deactivate");
    var var_suspend = createVariable("suspend");
    var var_resume = createVariable("resume");
    var var_start = createVariable("start");
    var var_done = createVariable("done");

    template.createEdge(loc_disabled, loc_enabled)
                                    .setSync(var_activate.getArrayExpression(var_id), false);
    

                                    
    template.createEdge(loc_enabled, loc_suspended)
                                    .setSync(var_suspend.getArrayExpression(var_id), false);
                                    
    template.createEdge(loc_suspended, loc_enabled)
                                    .setSync(var_resume.getArrayExpression(var_id), false);
    
    template.createEdge(loc_enabled, loc_done_1)
                                    .setSync(var_start.getArrayExpression(var_id), true);
                                    
    template.createEdge(loc_done_1, loc_done)
                                    .setSync(var_done.getArrayExpression(var_id), true);
                                    
    // Deactivation edges                        
    template.createEdge(loc_disabled, loc_disabled)
                                    .setSync(var_deactivate.getArrayExpression(var_id), false).setColor(Uppaal!ColorKind#LIGHTGREY);
   
    template.createEdge(loc_enabled, loc_disabled)
                                    .setSync(var_deactivate.getArrayExpression(var_id), false).setColor(Uppaal!ColorKind#LIGHTGREY);
                                    
    template.createEdge(loc_suspended, loc_disabled)
                                    .setSync(var_deactivate.getArrayExpression(var_id), false).setColor(Uppaal!ColorKind#LIGHTGREY);
    
    template.createEdge(loc_done, loc_disabled)
                                    .setSync(var_deactivate.getArrayExpression(var_id), false).setColor(Uppaal!ColorKind#LIGHTGREY);
    
    
    
    return template;
}



