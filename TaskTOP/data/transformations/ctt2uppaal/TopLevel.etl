operation createTopLevelTemplate() : Uppaal!Template {
    //// Create template
    var template = new Uppaal!Template();
    template.name = "TopLevel";
    
    //// Add declarations
    
    // Add elapsedTime clock
    var cl_elapsedTime = template.declareClock("elapsedTime");
    var cl_totalTime   = template.declareClock("totalTime");
    
    //// Add parameters
    
    // Create id paramater
    var var_root_task_id = createVariable("root_task_id");
    template.addParameter(createDataDeclaration(var_root_task_id), false); 
        
    //// Add locations
    
    // Add initial location
    var loc_init    = template.createLocation("Disabled", 0, 0).makeCommitted();
    var loc_waiting = template.createLocation("Waiting", 1, 0);
    var loc_running = template.createLocation("Running", 2, 0);
    var loc_done    = template.createLocation("Done", 3, 0);
    
    loc_done.setInvariant(createConjunction(cl_elapsedTime.setClockRate(0), cl_totalTime.setClockRate(0)));
    
    template.init = loc_init;
    
    //// Add edges
    
    // Add edge variables
    var var_activate = createVariable("activate");
    var var_start    = createVariable("start");
    var var_done     = createVariable("done");
    
    // Add actual edges
    template.createEdge(loc_init, loc_waiting)
                                    .setSync(var_activate.getArrayExpression(var_root_task_id), true)
                                    .addUpdate(cl_totalTime.simpleAssignment(0));
                                   
    template.createEdge(loc_waiting, loc_running)
                                    .setSync(var_start.getArrayExpression(var_root_task_id), false)
                                    .addUpdate(cl_elapsedTime.simpleAssignment(0));
    
    template.createEdge(loc_running, loc_done)
                                    .setSync(var_done.getArrayExpression(var_root_task_id), false);
                                    
    return template;
}